# - Config file for the ffs package
# It defines the following variables
#  FFS_INCLUDE_DIRS - include directories for ffs
#  FFS_LIBRARIES    - libraries to link against
#
# And the following imported targets:
#   ffs::ffs
 
include("${CMAKE_CURRENT_LIST_DIR}/ffs-config-version.cmake")

include(CMakeFindDependencyMacro)

set(_ffs_required_vars)
if(@FFS_USE_ATL@)
  find_dependency(ATL)
  list(APPEND _ffs_required_vars ATL_FOUND)
endif()

if(@FFS_USE_DILL@)
  find_dependency(DILL)
  list(APPEND _ffs_required_vars DILL_FOUND)
endif()
if(_ffs_required_vars)
  list(INSERT _ffs_required_vars 0 REQUIRED_VARS)
endif()

include(FindPackageHandleStandardArgs)
set(FFS_CONFIG "${CMAKE_CURRENT_LIST_FILE}")
find_package_handle_standard_args(FFS CONFIG_MODE ${_ffs_required_vars})

if(NOT TARGET ffs::ffs)
  include("${CMAKE_CURRENT_LIST_DIR}/ffs-targets.cmake")
endif()

set(FFS_LIBRARIES ffs::ffs)
set(FFS_INCLUDE_DIRS
  $<TARGET_PROPERTY:ffs::ffs,INTERFACE_INCLUDE_DIRECTORIES>
)
